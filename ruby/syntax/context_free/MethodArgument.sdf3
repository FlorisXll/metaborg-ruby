module MethodArgument

imports
  Command
  HashConstructor
  Method
  OperatorExpression

context-free syntax

  MethodArgument.MethodArgWithoutPar = ArgumentWithoutParentheses
//  MethodArgument.MethodArgWithPar =
  MethodArgument.IndexArgList = IndexingArgumentList
  
  IndexingArgumentList = Command
  IndexingArgumentList.OpExpList = OperatorExpressionList OptionalComma?
  IndexingArgumentList.SplatOpExpList = OperatorExpressionList "," SplattingArgument
  IndexingArgumentList.IndexAssocList = AssociationList OptionalComma?
  IndexingArgumentList.SplatOpExp = SplattingArgument
  OptionalComma.Comma = ","
  
  ArgumentWithParentheses.Pars = "("")"
  ArgumentWithParentheses.ArgList = <( <ArgumentList> )>
  ArgumentWithParentheses.OpExpList = 
    <( <OperatorExpressionList>, <ChainedCommandWithDoBlock> )>
  ArgumentWithParentheses.ArgsParChained =
    <( <ChainedCommandWithDoBlock> )>
  
  // Lookahead not in { '{' } 
  ArgumentWithoutParentheses.ArgWithoutPar = ArgumentList
  
  SplattingArgument.SplatArg = <* <OpExp>>
  SplattingArgumentOptional.SplatArgOptional = <, <SplattingArgument>>
  
  OperatorExpressionList.OpExpList = {OpExp ","}+  
  
  // TODO 
  ArgumentList.Block =  <<BlockArgument>>
  ArgumentList.Splatting = <<SplattingArgument> <BlockArgumentOptional?>>
  ArgumentList.OpList = 
    <<OperatorExpressionList>, <AssociationList> <SplattingArgumentOptional?> <BlockArgumentOptional?>>
  ArgumentList.OpWithoutAss = 
    <<OperatorExpressionList> <SplattingArgumentOptional?> <BlockArgumentOptional?>>
  ArgumentList.OpWithAss = 
    <<AssociationList> <SplattingArgumentOptional?> <BlockArgumentOptional?>>
  ArgumentList.Command = <<Command>>
  
  BlockArgument.BlockArg = <& <OpExp>>
  BlockArgumentOptional.BlockArgOptional = <, <BlockArgument>>