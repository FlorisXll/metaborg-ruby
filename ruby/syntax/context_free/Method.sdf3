module Method

imports
  Argument
  Block
  BodyStatement
  Break
  Command
  Expression
  Identifier
  Keyword
  MethodArgument
  Next
  Operator
  OperatorExpression
  Primary
  Return
  Separator
  Super
  Variable

context-free syntax
  MethodId.LocalVar = <<LOCAL-V-IDENTIFIER>>
  MethodId.Constant = <<CONSTANT-IDENTIFIER>>
  MethodId.MethodOnly = <<METHOD-ONLY-IDENTIFIER>>

  MethodName.Id = <<MethodId>>
  MethodName.MethodOp = <<OPERATOR-METHOD-NAME>>
  MethodName.Keyword = <<KEYWORD>>
  
  MethodIdNoCons.LocalVar = <<LOCAL-V-IDENTIFIER>>
  MethodIdNoCons.MethodOnly = <<METHOD-ONLY-IDENTIFIER>>

  MethodNameNoCons.Id = <<MethodId>>
  MethodNameNoCons.MethodOp = <<OPERATOR-METHOD-NAME>>
  MethodNameNoCons.Keyword = <<KEYWORD>>
  
  // Method name without Constant Identifier
  MethodNameExceptConstant = MethodNameNoCons
  
  PrimaryMethodInvocation = SuperWithOptionalArgument
  PrimaryMethodInvocation = IndexingMethodInvocation
  PrimaryMethodInvocation = METHOD-ONLY-IDENTIFIER
  PrimaryMethodInvocation.Block = <<MethodId> <Block>>
  PrimaryMethodInvocation.ArgumentsBlock = 
    <<MethodId> <ArgumentWithParentheses> 
     <Block?>>
  PrimaryMethodInvocation.MethodInvocArg =
    <<Primary> . <MethodName> <ArgumentWithParentheses?>
      <Block?>>
  PrimaryMethodInvocation.MethodInvocColons = 
    <<Primary> :: <MethodName> <ArgumentWithParentheses>
      <Block?>>
  PrimaryMethodInvocation.MethodInvocNoCons = 
    <<Primary> :: <MethodNameExceptConstant> <ArgumentWithParentheses>
      <Block?>>
  
  IndexingMethodInvocation.IndexMethodInvoc = <<Primary> [ <IndexingArgumentList?> ]>

  MethodInvocationWithoutParentheses.Command = <<Command>>
  MethodInvocationWithoutParentheses.ChainedCommand = <<ChainedCommandWithDoBlock>>
  MethodInvocationWithoutParentheses.ChainedCommandLong = 
    <<ChainedCommandWithDoBlock> <BlockSymbol> <MethodName> <ArgumentWithoutParentheses>>
  MethodInvocationWithoutParentheses.Return = <<ReturnWithArgument>>
  MethodInvocationWithoutParentheses.Break = <<BreakWithArgument>>
  MethodInvocationWithoutParentheses.Next = <<NextWithArgument>>
  
  BlockSymbol.Period = "."
  BlockSymbol.DoubleColon = "::"
  
  ChainedCommandWithDoBlock.ChainedCommand =
    <<CommandWithDoBlock> <ChainedMethodInvocation*>>
  
  ChainedMethodInvocation.ChainedMethodInvoc = <<BlockSymbol> <MethodName>>
  ChainedMethodInvocation.ChainedMethodInvoc = 
    <<BlockSymbol> <MethodName> <ArgumentWithParentheses>>
    
  CommandWithDoBlock = SuperWithArgumentAndDoBlock
  CommandWithDoBlock = 
    <<MethodId> <ArgumentWithoutParentheses> <DoBlock>>
  CommandWithDoBlock =
    <<Primary> <BlockSymbol> <MethodName> <ArgumentWithoutParentheses> <DoBlock>>
    
  
  
  // Primary Expression Method definition
  
  MethodDef.Method = 
    <def <DefinedMethodName> <MethodParamaters>
      <MethodBody>
     end>
  DefinedMethodName.DefMethodName = MethodName
  DefinedMethodName.AssignMethodName = ASSIGNMENT-LIKE-METHOD-IDENTIFIER
  MethodBody.Body = BStmt
  
  SingletonMethodDef.SingletonMethod = 
    <def <Singleton> <SingSep> <DefinedMethodName> <MethodParamaters>
      <MethodBody>
     end>
  Singleton.Exp = <(<Exp>)>
  Singleton.VarRef = Var
  SingSep.Period = "."
  SingSep.Colons = "::"    
  
  MethodParamaters.ParamsWithParantheses = <( <ParamList?> )>
  MethodParamaters.Params = <<ParamList?> <Sep>>
  
  ParamList.MandatoryParams =<<MandatoryParams> <OptionalOpt?> <OptionalArray?> <OptionalProc?>>
  ParamList.Optional = <<OptionalParams> <OptionalArray?> <OptionalProc?>>
  ParamList.Array = <<ArrayParams> <OptionalProc?>>
  ParamList.Proc = ProcParams
  
  MandatoryParams.Params = {MandatoryParam ","}+
  MandatoryParam.MandatoryParam = LOCAL-V-IDENTIFIER
  
  OptionalParams.Params = {OptionalParam ","}+
  OptionalParam.OptionalParam = <<OptionalParamName> = <OpExp>>
  OptionalParamName.Name = LOCAL-V-IDENTIFIER
  OptionalOpt.OptionalOptionalParams = <, <OptionalParams>>
  
  ArrayParams.ArrayParamsWithName = <* <ArrayParamName>>
  ArrayParams.ArrayParamStar = "*"
  ArrayParamName.Name = LOCAL-V-IDENTIFIER
  OptionalArray.OptionalArray = <, <ArrayParams>>
  
  ProcParams.ProcParam = <& <ProcParamName>>
  ProcParamName.ProcName = LOCAL-V-IDENTIFIER
  OptionalProc.OptionalProc = <, <ProcParams>>
  

