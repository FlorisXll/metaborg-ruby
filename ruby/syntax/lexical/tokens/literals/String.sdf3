module String

imports

  Separator
  Identifier
  CompoundStatement

lexical syntax

  // Sources:
  // - https://github.com/ruby/ruby/blob/trunk/doc/syntax/literals.rdoc#strings
  // - http://ruby-doc.org/core-2.5.0/doc/syntax/literals_rdoc.html#label-Strings
  // - https://www.ipa.go.jp/files/000011432.pdf (chapter 8.7.6.3)

  STRING = SingleQuotedString
  
  // Single Quoted String
  
  SingleQuotedString = [\'] SingleQuotedStringCharacter* [\']
  
  SingleQuotedStringCharacter = SingleQuotedStringNonEscapedCharacter
  SingleQuotedStringCharacter = SingleQuotedEscapeSequence
  
  SingleQuotedEscapeSequence = SingleEscapeCharacterSequence
  SingleQuotedEscapeSequence = SingleQuotedStringNonEscapedCharacterSequence
  
  SingleEscapeCharacterSequence = [\\] SingleQuotedStringMetaCharacter
  SingleQuotedStringNonEscapedCharacterSequence = [\\] SingleQuotedStringNonEscapedCharacter  
  
  SingleQuotedStringMetaCharacter = [\'\\]
  SingleQuotedStringNonEscapedCharacter = ~[\'\\]
  
  // Double Quoted String
  
  DoubleQuotedString = [\"] DoubleQuotedStringCharacter* [\"]
  
  DoubleQuotedStringCharacter = ~[\"\#\\]
  DoubleQuotedStringCharacter = NonInterpolationHashSymbol
  DoubleQuotedStringCharacter = DoubleEscapeSequence
  DoubleQuotedStringCharacter = InterpolatedCharacterSequence
  
  DoubleEscapeSequence = SimpleEscapeSequence
  DoubleEscapeSequence = NonEscapedSequence
  DoubleEscapeSequence = LineTerminatorEscapeSequence
  DoubleEscapeSequence = OctalEscapeSequence
  DoubleEscapeSequence = HexadecimalEscapeSequence
  DoubleEscapeSequence = ControlEscapeSequence
  
  SimpleEscapeSequence = [\\] DoubleEscapedCharacter
  NonEscapedSequence = [\\] NonEscapedDoubleQuotedStringCharacter
  OctalEscapeSequence = [\\] [0-7] [0-7]? [0-7]?
  HexadecimalEscapeSequence = [\\] "x" [0-9a-fA-F] [0-9a-fA-F]?
  ControlEscapeSequence = [\\] ControlCharacter ControlEscapedCharacter
  MetaEscapeSequence = [\\] MetaCharacter MetaEscapedCharacter
  MetaControlEscapeSequence = [\\] MetaControlCharacter MetaControlEscapedCharacter
  InterpolatedCharacterSequence = "#" GLOBAL-V-IDENTIFIER
  InterpolatedCharacterSequence = "#" CLASS-V-IDENTIFIER
  InterpolatedCharacterSequence = "#" INSTANCE-V-IDENTIFIER
  InterpolatedCharacterSequence = "#{" CStmt "}"
  
  ControlCharacter = "c"
  ControlCharacter = "C-"
  ControlEscapedCharacter = DoubleEscapeSequence
  ControlEscapedCharacter = "?"
  ControlEscapedCharacter = ~[\\\?]
  
  MetaCharacter = "M-"
  MetaEscapedCharacter = DoubleEscapeSequence
  MetaEscapedCharacter = ~[\\]
  
  MetaControlCharacter = MetaCharacter ControlCharacter
  MetaControlCharacter = ControlCharacter MetaCharacter
  MetaControlEscapedCharacter = DoubleEscapeSequence
  MetaControlEscapedCharacter = ~[\\]
  
  DoubleEscapedCharacter = [\\ntrfvaebs]
  NonEscapedDoubleQuotedStringCharacter = ~[a-zA-Z0-9]
  NonEscapedDoubleQuotedStringCharacter = LINETERMINATOR {reject}
  
  NonInterpolationHashSymbol = "#"
  
lexical restrictions

  NonInterpolationHashSymbol -/- [\$\@\{]